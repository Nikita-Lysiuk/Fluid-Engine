#version 460
#extension GL_GOOGLE_include_directive : enable

#include "../include/common.glsl"

layout(local_size_x = 256) in;

layout(set = 0, binding = 0, std430) buffer PosBuffer {
    vec4 p[];
} positions;

layout(set = 0, binding = 1, std430) buffer EntriesBuffer {
    Entry entries[];
} grid_entries;


layout(push_constant) uniform PushConstants {
    uint num_particles;
    uint table_size;
} pc;


void main() {
    uint i = gl_GlobalInvocationID.x;
    if (i >= pc.table_size) return;

    if (i < pc.num_particles) {
        vec3 pos = positions.p[i].xyz;
        ivec3 grid_pos = ivec3(floor(pos / sim_params.smoothing_radius));

        grid_entries.entries[i].hash = get_cell_hash(grid_pos, pc.table_size);
        grid_entries.entries[i].index = i;
    } else {
        grid_entries.entries[i].hash = 0xFFFFFFFF;
        grid_entries.entries[i].index = 0xFFFFFFFF;
    }
}