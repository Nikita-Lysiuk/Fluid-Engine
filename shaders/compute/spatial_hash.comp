#version 460
#extension GL_GOOGLE_include_directive : enable

#include "../include/common.glsl"

layout(local_size_x = 256) in;

layout(set = 0, binding = 0, std430) buffer PosBuffer {
    vec4 p[];
} positions;

layout(set = 0, binding = 1, std430) buffer EntriesBuffer {
    Entry entries[];
} grid_entries;

layout(set = 0, binding = 2, std140) uniform Params {
    float particle_radius;
    float particle_mass;
    float smoothing_radius;
    float target_density;
    vec4 gravity;
    vec4 box_min;
    vec4 box_max;
} params;

layout(push_constant) uniform PushConstants {
    uint num_particles;
    uint table_size;
} pc;

uint get_hash(ivec3 grid_pos) {
    uint p1 = 73856093;
    uint p2 = 19349663;
    uint p3 = 83492791;

    return (uint(grid_pos.x) * p1 ^ uint(grid_pos.y) * p2 ^ uint(grid_pos.z) * p3) % pc.table_size;
}

void main() {
    uint i = gl_GlobalInvocationID.x;
    if (i >= pc.table_size) return;

    if (i < pc.num_particles) {
        vec3 pos = positions.p[i].xyz;
        ivec3 grid_pos = ivec3(floor(pos / params.smoothing_radius));

        grid_entries.entries[i].hash = get_hash(grid_pos);
        grid_entries.entries[i].index = i;
    } else {
        grid_entries.entries[i].hash = 0xFFFFFFFF;
        grid_entries.entries[i].index = 0xFFFFFFFF;
    }
}